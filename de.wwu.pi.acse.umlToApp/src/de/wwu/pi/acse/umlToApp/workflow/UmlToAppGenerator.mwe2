module de.wwu.pi.acse.umlToApp.workflow.UmlToAppGenerator

import org.eclipse.emf.mwe.utils.*

var targetDir = "src-gen"
var projectName = "PizzaOrdering"
var baseProjectPath = "../${projectName}"
var modelPath = "../de.wwu.pi.acse.pizza.model"

Workflow {
	bean = StandaloneSetup {
		//register UML metamodel
		registerGeneratedEPackage = "org.eclipse.uml2.uml.UMLPackage"
		extensionMap = {
			from = "uml"
			to = "org.eclipse.uml2.uml.internal.resource.UMLResourceFactoryImpl"
		}
		uriMap = {
			from = "pathmap://UML_LIBRARIES/"
			to = "jar:file:res/org.eclipse.uml2.uml.resources_5.0.2.v20150202-0947.jar!/libraries/"
		}
		uriMap = {
			from = "pathmap://UML_METAMODELS/"
			to = "jar:file:res/org.eclipse.uml2.uml.resources_5.0.2.v20150202-0947.jar!/metamodels/"
		}
		uriMap = {
			from = "pathmap://UML_PROFILES/"
			to = "jar:file:res/org.eclipse.uml2.uml.resources_5.0.2.v20150202-0947.jar!/profiles/"
		}
	}
	
//	Problem with reading file encoding https://bugs.eclipse.org/bugs/show_bug.cgi?id=409069
	component = DirectoryCleaner {
		directory = targetDir
		exclude = ".git"
	}

	component = DirectoryCleaner {
		directory = "${baseProjectPath}-EJB/${targetDir}"
		exclude = ".git"
		exclude = ".gitkeep"
	}

	component = DirectoryCleaner {
		directory = "${baseProjectPath}-Persistence/${targetDir}"
		exclude = ".git"
		exclude = ".gitkeep"
	}

	component = DirectoryCleaner {
		directory = "${baseProjectPath}-Web/${targetDir}"
		exclude = ".git"
		exclude = ".gitkeep"
	}
	
	component = UmlToAppGeneratorSupport {}
	
	//register uml reader
	component = org.eclipse.xtext.mwe.Reader {
		path = modelPath
		register = UmlToAppGeneratorSetup {}
		loadResource = {
			slot = "model"
		}
	}
	
	//register generator
	component = org.eclipse.xtext.generator.GeneratorComponent {
		register = UmlToAppGeneratorSetup {}
		slot = "model"
		outlet = {
			path = targetDir
		}
	}
}